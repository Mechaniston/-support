
#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Период", Период);
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		
		Период = НачалоМесяца(ТекущаяДатаСеанса());
		
	КонецЕсли;
	
	УстановитьРежимВыбораПериода(ЭтаФорма, Параметры.РежимВыбораПериода);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОтображение();
	
КонецПроцедуры
	
#КонецОбласти 

#Область ОбработчикиДействийПользователя

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ИзменилсяГод = Истина;
	УстановитьПериод(Год(Период), Месяц(Период));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ГодУменьшить(Команда)
	
	ВыбираемыйГод = Год(Период) - 1;
	УстановитьПериод(ВыбираемыйГод, Месяц(Период));
	
КонецПроцедуры

&НаКлиенте
Процедура ГодУвеличить(Команда)
	
	ВыбираемыйГод = Год(Период) + 1;
	УстановитьПериод(ВыбираемыйГод, Месяц(Период));
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц01(Команда)
	
	УстановитьПериод(Год(Период), 1);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц02(Команда)
	
	УстановитьПериод(Год(Период), 2);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц03(Команда)
	
	УстановитьПериод(Год(Период), 3);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц04(Команда)
	
	УстановитьПериод(Год(Период), 4);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц05(Команда)
	
	УстановитьПериод(Год(Период), 5);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц06(Команда)
	
	УстановитьПериод(Год(Период), 6);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц07(Команда)
	
	УстановитьПериод(Год(Период), 7);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц08(Команда)
	
	УстановитьПериод(Год(Период), 8);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц09(Команда)
	
	УстановитьПериод(Год(Период), 9);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц10(Команда)
	
	УстановитьПериод(Год(Период), 10);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц11(Команда)
	
	УстановитьПериод(Год(Период), 11);
	
КонецПроцедуры

&НаКлиенте
Процедура Месяц12(Команда)
	
	УстановитьПериод(Год(Период), 12);
	
КонецПроцедуры

&НаКлиенте
Процедура Квартал1(Команда)
	
	УстановитьПериод(Год(Период), 1);
	
КонецПроцедуры

&НаКлиенте
Процедура Квартал2(Команда)
	
	УстановитьПериод(Год(Период), 4);
	
КонецПроцедуры

&НаКлиенте
Процедура Квартал3(Команда)
	
	УстановитьПериод(Год(Период), 7);
	
КонецПроцедуры

&НаКлиенте
Процедура Квартал4(Команда)
	
	УстановитьПериод(Год(Период), 10);
	
КонецПроцедуры

&НаКлиенте
Процедура Полугодие1(Команда)
	
	УстановитьПериод(Год(Период), 1);
	
КонецПроцедуры

&НаКлиенте
Процедура Полугодие2(Команда)
	
	УстановитьПериод(Год(Период), 7);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыполнитьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры
	
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьРежимВыбораПериода(Форма, Знач РежимВыбора)
	
	Если НЕ ЗначениеЗаполнено(РежимВыбора) Тогда
		
		РежимВыбора = "Месяц";
		
	КонецЕсли;
	
	Если Форма.РежимВыбораПериода = ВРег(РежимВыбора) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Форма.РежимВыбораПериода = ВРег(РежимВыбора);
	
	ГруппаМесяцыВидимость = Ложь;
	ГруппаКварталыВидимость = Ложь;
	ГруппаПолугодияВидимость = Ложь;
		
	Если Форма.РежимВыбораПериода = "МЕСЯЦ" Тогда
		
		ГруппаМесяцыВидимость = Истина;
		Форма.Период = НачалоМесяца(Форма.Период);
		
	ИначеЕсли Форма.РежимВыбораПериода = "КВАРТАЛ" Тогда
		
		ГруппаКварталыВидимость = Истина;
		НомерКвартала = Цел((Месяц(Форма.Период) - 1) / 3 + 1);
		Форма.Период = Дата(Год(Форма.Период), (НомерКвартала - 1) * 3 + 1, 1);
		
	ИначеЕсли Форма.РежимВыбораПериода = "ПОЛУГОДИЕ" Тогда
		
		ГруппаПолугодияВидимость = Истина;
		Форма.Период = Дата(Год(Форма.Период), ?(Месяц(Форма.Период) < 7, 1, 7), 1);
		
	КонецЕсли;
	
	Форма.Элементы.ГруппаМесяцы.Видимость = ГруппаМесяцыВидимость;
	Форма.Элементы.ГруппаКварталы.Видимость = ГруппаКварталыВидимость;
	Форма.Элементы.ГруппаПолугодия.Видимость = ГруппаПолугодияВидимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображение()
	
	Если РежимВыбораПериода = "МЕСЯЦ" Тогда
		
		Для НомерМесяца = 1 По 12 Цикл
			
			КнопкаМесяца = Элементы["Месяц" + Формат(НомерМесяца, "ЧЦ=2; ЧВН=")];
			
			КнопкаМесяца.Шрифт = Новый Шрифт(КнопкаМесяца.Шрифт, , , НомерМесяца = Месяц(Период));
			
		КонецЦикла;
		
		ПредставлениеПериода = Даты_КлиентСервер_ат.ПолучитьПредставлениеМесяца(Период);
		
	ИначеЕсли РежимВыбораПериода = "КВАРТАЛ" Тогда
		
		КварталМесяца = (Месяц(Период) - 1) / 3 + 1;
		
		Для НомерКвартала = 1 По 4 Цикл
			
			КнопкаКвартала = Элементы["Квартал" + НомерКвартала];
			
			КнопкаКвартала.Шрифт = Новый Шрифт(КнопкаКвартала.Шрифт, , , НомерКвартала = КварталМесяца);
			
		КонецЦИкла;
		
		ПредставлениеПериода = Даты_КлиентСервер_ат.ПолучитьПредставлениеКвартала(Период);
		
	Иначе
		
		ПолугодиеМесяца = ?(Месяц(Период) <= 6, 1, 2);
		
		Элементы.Полугодие1.Шрифт = Новый Шрифт(Элементы.Полугодие1.Шрифт, , , ПолугодиеМесяца = 1);
		Элементы.Полугодие2.Шрифт = Новый Шрифт(Элементы.Полугодие2.Шрифт, , , ПолугодиеМесяца = 2);
		
		ПредставлениеПериода = Даты_КлиентСервер_ат.ПолучитьПредставлениеПолугодия(Период);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПериод(Год, Месяц)
	
	Если Год = Год(Период) И Месяц = Месяц(Период) И НЕ ИзменилсяГод Тогда
		
		ВыполнитьВыбор();
		
	КонецЕсли;
	
	Если Год < 1 Тогда
		
		Год = 1;
		
	КонецЕсли;
	
	ИзменилсяГод = Ложь;
	Период = Дата(Год, Месяц, 1);
	
	ОбновитьОтображение();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор()
	
	Закрыть(Новый Структура("Период, ПредставлениеПериода", Период, ПредставлениеПериода));
	
КонецПроцедуры
	
#КонецОбласти
