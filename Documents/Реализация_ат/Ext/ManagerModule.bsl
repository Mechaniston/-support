 
Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("ВнешнийНомер");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ПустаяСтрока(Данные.ВнешнийНомер) Тогда
		
		Попытка
			
			ВнешнийНомерКакЧисло = Число(Данные.ВнешнийНомер);
			Если ВнешнийНомерКакЧисло = 0 Тогда
				
				Если ПустаяСтрока(Данные.Номер) Тогда
					ВнешнийНомер = "новая";
				Иначе
					ВнешнийНомер = Формат(Число(Данные.Номер), "ЧГ=");
				КонецЕсли;
				
			Иначе
				ВнешнийНомер = Данные.ВнешнийНомер;
			КонецЕсли;
			
		Исключение
			ВнешнийНомер = Данные.ВнешнийНомер;
		КонецПопытки;
		
	Иначе
		
		Если ПустаяСтрока(Данные.Номер) Тогда
			ВнешнийНомер = "новая";
		Иначе
			ВнешнийНомер = Формат(Число(Данные.Номер), "ЧГ=");
		КонецЕсли;
		
	КонецЕсли;
	
	Представление = "Реализация " + ВнешнийНомер + " от " + Формат(Данные.Дата, "ДФ=dd.MM.yy");
	
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СинхронизированныеПечатныеФормы_ат.РегистрСинхронизации,
		|	СинхронизированныеПечатныеФормы_ат.ИмяМакета,
		|	СинхронизированныеПечатныеФормы_ат.Наименование,
		|	СинхронизированныеПечатныеФормы_ат.ВнешняяПечатнаяФорма,
		|	СинхронизированныеПечатныеФормы_ат.Порядок,
		|	СинхронизированныеПечатныеФормы_ат.ТипФайлаПоУмолчаниюСтрокой,
		|	СинхронизированныеПечатныеФормы_ат.ИспользоватьДляОтправки,
		|	СинхронизированныеПечатныеФормы_ат.ШаблонТекстаПисьма
		|ИЗ
		|	Справочник.СинхронизированныеПечатныеФормы_ат КАК СинхронизированныеПечатныеФормы_ат
		|ГДЕ
		|	НЕ СинхронизированныеПечатныеФормы_ат.ПометкаУдаления
		|	И СинхронизированныеПечатныеФормы_ат.РегистрСинхронизации = &РегистрСинхронизации";
	
	Запрос.УстановитьПараметр("РегистрСинхронизации", Перечисления.РегистрыСинхронизацииДокументов_ат.СинхронизацияРеализаций_ат);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = Выборка.ИмяМакета;
		КомандаПечати.Представление = Выборка.Наименование;
		КомандаПечати.СписокФорм	= "ФормаСпискаИВыбора,ФормаДокумента";
		КомандаПечати.Порядок		= Выборка.Порядок;
		
		Если НЕ ПустаяСтрока(Выборка.ТипФайлаПоУмолчаниюСтрокой) Тогда
			КомандаПечати.ФорматСохранения = ТипФайлаТабличногоДокумента[Выборка.ТипФайлаПоУмолчаниюСтрокой];
		КонецЕсли;
		
		КомандаПечати.ДополнительныеПараметры.Вставить("РегистрСинхронизации",		Выборка.РегистрСинхронизации);
		КомандаПечати.ДополнительныеПараметры.Вставить("ВнешняяПечатнаяФорма",		Выборка.ВнешняяПечатнаяФорма);
		КомандаПечати.ДополнительныеПараметры.Вставить("ИспользоватьДляОтправки",	Выборка.ИспользоватьДляОтправки);
		
		Если НЕ ПустаяСтрока(Выборка.ШаблонТекстаПисьма) Тогда
			КомандаПечати.ДополнительныеПараметры.Вставить("ШаблонТекстаПисьма",	Выборка.ШаблонТекстаПисьма);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт //!!!TODO: сделать одну процедуру для счетов и актов
	
	Финансы_ат.ПечатьФинДокумента(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

Функция   ПолучитьПланируемуюДатуПодписания(Дата, Договор) Экспорт
	
	СрокПодписанияВДнях = ?(Договор.Родитель.Пустая(), Договор.СрокПодписанияРеализации, Договор.Родитель.СрокПодписанияРеализации); 
	
	Если СрокПодписанияВДнях = 0 Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		Возврат ПродолжительностьПроцессов_Сервер_ат.ПолучитьДату(?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()), СрокПодписанияВДнях);
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаменитьПараметрыВТекстеПисьма(Реализация, Текст, КоличествоОтправляемых = 1, РазныеДоговоры = Ложь, ОдинаковыйПериод = Истина) Экспорт
	
	Параметр = "&Реализация";
	Если СтрНайти(Текст, Параметр) > 0 Тогда
		
		Текст = СтрЗаменить(Текст, Параметр, ?(КоличествоОтправляемых > 1, "Реализации", "Реализация"));
		
	КонецЕсли;
	
	Уведомления_ат.ЗаменитьПараметрыВТекстеПисьмаФинансовогоДокумента(Реализация, Текст, КоличествоОтправляемых, РазныеДоговоры, ОдинаковыйПериод);
	
КонецПроцедуры

Функция   ПолучитьТемуПисьма(Документ, КоличествоОбъектов, ПредставлениеПериода = "") Экспорт
	
	Возврат Уведомления_ат.ПолучитьТемуПисьмаДляОтправкиДокумента(Документ, "Реализации", КоличествоОбъектов, ПредставлениеПериода);
	
КонецФункции
