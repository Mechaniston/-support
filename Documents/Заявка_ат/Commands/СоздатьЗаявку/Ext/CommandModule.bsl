
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДополнительныеПараметры = Новый Структура("ПараметрКоманды,ПараметрыВыполненияКоманды", ПараметрКоманды, ПараметрыВыполненияКоманды);
	
	Если ПоПисьмуСуществуютЗаявки(ПараметрКоманды) Тогда
		
		СписокОтветов = Новый СписокЗначений;
		СписокОтветов.Добавить("Создать", "Создать заявку");
		СписокОтветов.Добавить("Перейти", "Перейти к списку заявок");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОПереходеКСпискуЗаявок", ЭтотОбъект,
			ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, "По письму уже созданы заявки!
											|Перейти к списку?", СписокОтветов);
		
	Иначе
		
		ПослеВопросаОПереходеКСпискуЗаявок("Создать", ДополнительныеПараметры);
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция   ПоПисьмуСуществуютЗаявки(Письмо)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Заявка_ат.Ссылка
	|ИЗ
	|	Документ.Заявка_ат КАК Заявка_ат
	|ГДЕ
	|	Заявка_ат.Основание = &Основание";
	
	Запрос.УстановитьПараметр("Основание", Письмо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

&НаКлиенте
Процедура ПослеВопросаОПереходеКСпискуЗаявок(Результат, ДополнительныеПараметры)
	
	ПараметрыВыполненияКоманды = ДополнительныеПараметры.ПараметрыВыполненияКоманды;
	
	Если Результат = "Создать" Тогда
		
		ПараметрыФормы = Новый Структура("Основание", ДополнительныеПараметры.ПараметрКоманды);
		ОткрытьФорму("Документ.Заявка_ат.ФормаОбъекта", ПараметрыФормы,
			ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
		
	ИначеЕсли Результат = "Перейти" Тогда
		
		ПараметрыФормы = Новый Структура("Ключ,ПоказатьЗаявки", ДополнительныеПараметры.ПараметрКоманды, Истина);
		
		Форма = ПолучитьФорму("Документ.ЭлектронноеПисьмоВходящее.ФормаОбъекта", ПараметрыФормы);
		
		Если НЕ Форма = Неопределено Тогда
			
			Если Форма.Открыта() Тогда
				
				Форма.Активизировать();
				Оповестить("ПоказатьЗаявки",, ПараметрыВыполненияКоманды.Источник);
				
			Иначе
				
				Форма.Открыть()
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
