
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	//СвойстваЗаявки = Планирование_Сервер_ат.ПолучитьСвойстваЗаявки(ПараметрКоманды);
	//Если СвойстваЗаявки.Свойство("РезультатВыполненияВHTML") Тогда
	//	
	//	РезультатВыполнения = СвойстваЗаявки.РезультатВыполненияВHTML;
	//	
	//Иначе
	//	
	//	РезультатВыполнения = "";
	//	
	//КонецЕсли;
	//
	
	РезультатВыполнения = СформироватьРезультатЗаявок(ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура("ТекстHTML", РезультатВыполнения);
	ПараметрыФормы.Вставить("ФормаОткрытаДляПросмотра", Истина);
	ПараметрыФормы.Вставить("Заголовок", "Результат Заявок");
	
	ОткрытьФорму("ОбщаяФорма.ФормаРедактораHTML_ат", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
КонецПроцедуры

&НаСервере
Функция СформироватьРезультатЗаявок(Заявки)
	
	ДокументHTML = Новый ДокументHTML;
	
	ЭлементТело = ДокументHTML.СоздатьЭлемент("body");
	ДокументHTML.Тело = ЭлементТело;
	
	ЭлементБлок = ДокументHTML.СоздатьЭлемент("p");
	ЭлементТело.ДобавитьДочерний(ЭлементБлок);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Заявка_ат.Ссылка КАК Заявка,
	|	СвойстваЗаявок_ат.РезультатВыполненияВHTML,
	|	ТипыЗаявок_ат.Ссылка КАК ТипЗаявки
	|ИЗ
	|	Справочник.ТипыЗаявок_ат КАК ТипыЗаявок_ат
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Заявка_ат КАК Заявка_ат
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваЗаявок_ат КАК СвойстваЗаявок_ат
	|			ПО (СвойстваЗаявок_ат.Ссылка = Заявка_ат.Ссылка)
	|		ПО (Заявка_ат.ТипЗаявки = ТипыЗаявок_ат.Ссылка)
	|ГДЕ
	//|	НЕ ТипыЗаявок_ат.Срочное
	//|	И НЕ ТипыЗаявок_ат.Исправление
	//|	И НЕ ТипыЗаявок_ат.Обновление
	//|	И 
	|	Заявка_ат.Ссылка В(&Заявки)
	|	И НЕ ПОДСТРОКА(СвойстваЗаявок_ат.РезультатВыполненияВHTML, 1, 10) = """"
	|ИТОГИ ПО
	|	ТипЗаявки";
	
	Запрос.УстановитьПараметр("Заявки", Заявки);
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		ТипЗаявки = Выборка.Типзаявки;
		
		ЭлементDIV = Взаимодействия.ДобавитьЭлементСАтрибутами(ЭлементБлок, "div",
			Новый Структура("style", "color: #880000"));
		
		Взаимодействия.ДобавитьТекстовыйУзел(ЭлементDIV, Строка(ТипЗаявки), Истина, Истина);
		
		ВыборкаПоТипуЗаявки = Выборка.Выбрать();
		
		Пока ВыборкаПоТипуЗаявки.Следующий() Цикл
			
			ЭлементDIV = Взаимодействия.ДобавитьЭлементСАтрибутами(
				ЭлементБлок,
				"div",
				Новый Структура("style", "margin:10pt"));
			
			Заявка = ВыборкаПоТипуЗаявки.Заявка;
			Результат = ВыборкаПоТипуЗаявки.РезультатВыполненияВHTML;
			
			Взаимодействия.ДобавитьТекстовыйУзел(ЭлементDIV, Строка(Заявка), Истина, Истина);
			
			ЭлементDIVСТекстом = Взаимодействия.ДобавитьЭлементСАтрибутами(
				ЭлементDIV,
				"div",
				Новый Структура("style", "margin:10pt"));
				
			РаботаСHTML_Сервер_ат.ИмпортироватьДокументВЭлемент(ДокументHTML, ЭлементDIVСТекстом, Взаимодействия.ПолучитьОбъектДокументHTMLИзТекстаHTML(Результат));
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Взаимодействия.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	
КонецФункции