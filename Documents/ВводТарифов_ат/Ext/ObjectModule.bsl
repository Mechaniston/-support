
Процедура ОбработкаПроведения(Отказ, Режим)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МАКСИМУМ(Тарифы_атСрезПоследних.Период) КАК МаксимальнаяДата,
		|	НЕОПРЕДЕЛЕНО КАК Дубль
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	РегистрСведений.Тарифы_ат.СрезПоследних(
		|			,
		|			Организация = &Организация
		|				И ВидДоговора = &ВидДоговора
		|				И Клиент = ЗНАЧЕНИЕ(Справочник.Контрагенты_ат.ПустаяСсылка)) КАК Тарифы_атСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДАТАВРЕМЯ(1, 1, 1),
		|	Тарифы_атСрезПоследних.Регистратор
		|ИЗ
		|	РегистрСведений.Тарифы_ат.СрезПоследних(
		|			&Период,
		|			Организация = &Организация
		|				И ВидДоговора = &ВидДоговора
		|				И Клиент = &Клиент
		|				И Договор = &Договор
		|				И Проект = &Проект) КАК Тарифы_атСрезПоследних
		|ГДЕ
		|	Тарифы_атСрезПоследних.Период = &Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ЕСТЬNULL(ВТ.МаксимальнаяДата, ДАТАВРЕМЯ(1, 1, 1))) КАК МаксимальнаяДата,
		|	МАКСИМУМ(ВТ.Дубль) КАК Дубль
		|ИЗ
		|	ВТ КАК ВТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ";

	Запрос.УстановитьПараметр("Период", НачалоДействия);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидДоговора", ВидДоговора);
	Запрос.УстановитьПараметр("Клиент", Клиент);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Если ЗначениеЗаполнено(Выборка.Дубль) Тогда
		
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Существует проведённый Ввод тарифов с аналогичными ключевыми параметрами: " + Выборка.Дубль;
		Сообщение.Сообщить(); 
		
		Возврат;
		
	ИначеЕсли НачалоДействия < Выборка.МаксимальнаяДата Тогда
		
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Существует проведённый Ввод тарифов с датой начала действия " + Формат(Выборка.МаксимальнаяДата, "ДФ=dd.MM.yyyy");
		Сообщение.Сообщить(); 
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Тарифы_атСрезПоследних.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.Тарифы_ат.СрезПоследних(
		|			,
		|			Организация = &Организация
		|				И ВидДоговора = &ВидДоговора
		|				И НЕ Номенклатура В (&Номенклатуры)) КАК Тарифы_атСрезПоследних
		|ГДЕ
		|	НЕ Тарифы_атСрезПоследних.ОкончаниеДействия > &НачалоДействия";

	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидДоговора", ВидДоговора);
	Запрос.УстановитьПараметр("Номенклатуры", Тарифы.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("НачалоДействия", НачалоДействия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НадоПроверитьПериод = Истина;
	
	Движения.Тарифы_ат.Записывать = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		ДобавитьДвижение(Выборка.Номенклатура, 0, 0, НачалоДействия - 1);
		
	КонецЦикла;
	 
	Для Каждого Строка Из Тарифы Цикл
		
		СтрокаТарифныйПлан = ТарифныеПланы[Строка.НомерСтрокиТарифногоПлана - 1];
		
		ТарифныйПлан = ?(ПустаяСтрока(Наименование), "", Наименование + "/")
			+ ?(ПустаяСтрока(СтрокаТарифныйПлан.НазваниеТарифногоПлана), "От " + СтрокаТарифныйПлан.ЗначениеВыбора, СтрокаТарифныйПлан.НазваниеТарифногоПлана);
		
		ДобавитьДвижение(Строка.Номенклатура, СтрокаТарифныйПлан.ЗначениеВыбора, Строка.Цена, ОкончаниеДействия, ТарифныйПлан);
		
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьДвижение(Номенклатура, ЗначениеВыбора, Сумма, ДатаОкончания, ТарифныйПлан = "")
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	Движение = Движения.Тарифы_ат.Добавить();
	Движение.Период 			= НачалоДействия;
	Движение.Номенклатура 		= Номенклатура;
	Движение.ЗначениеВыбора 	= ЗначениеВыбора;
	Движение.Стоимость 			= Сумма;
	Движение.ОкончаниеДействия 	= ДатаОкончания;
	Движение.Организация 		= Организация;
	Движение.ВидДоговора 		= ВидДоговора;
	Движение.Клиент 			= Клиент;
	Движение.Договор 			= Договор;
	Движение.Проект 			= Проект;
	Движение.ТарифныйПлан 		= ТарифныйПлан;
	
КонецПроцедуры 
