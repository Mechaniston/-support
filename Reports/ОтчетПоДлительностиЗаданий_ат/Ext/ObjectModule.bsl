
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Если КомпоновщикНастроек.Настройки.Структура.Количество() > 1 Тогда
		КомпоновщикНастроек.Настройки.Структура.Удалить(1);
	КонецЕсли;
	ЭтоПерваяГруппировка = Истина;
	
	Для Каждого Группировка Из Группировки Цикл
		Если Группировка.Пометка И ЭтоПерваяГруппировка Тогда
			НоваяГруппировка = КомпоновщикНастроек.Настройки.Структура.Вставить(1, Тип("ГруппировкаКомпоновкиДанных"));
			НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			ПолеГруппировки = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировки.Поле = Группировка.Значение;		
			НоваяГруппировка1 = НоваяГруппировка;
			ЭтоПерваяГруппировка = Ложь;
		ИначеЕсли Группировка.Пометка Тогда	
			НоваяГруппировка1 = НоваяГруппировка1.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
			НоваяГруппировка1.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НоваяГруппировка1.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			ПолеГруппировки = НоваяГруппировка1.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировки.Поле = Группировка.Значение;
		КонецЕсли;
	КонецЦикла;
	
	ДетальныеЗаписи = Неопределено;
	Для Каждого Поле Из Поля Цикл
		Если Поле.Пометка Тогда 
			Если ДетальныеЗаписи = Неопределено Тогда
				Если ЭтоПерваяГруппировка Тогда
					ДетальныеЗаписи = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
				Иначе
					ДетальныеЗаписи = НоваяГруппировка1.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
				КонецЕсли;
				ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			КонецЕсли;
			НовоеПоле = ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			НовоеПоле.Поле = Поле.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Если КомпоновщикНастроек.Настройки.Структура.Количество() = 1 Тогда
		КомпоновщикНастроек.Настройки.Структура[0].Использование = Истина;
	Иначе	
		КомпоновщикНастроек.Настройки.Структура[0].Использование = Ложь;
	КонецЕсли;
	
КонецПроцедуры
