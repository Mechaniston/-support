#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура УстановкаПараметровСеанса(ИменаПараметровСеанса)
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыСервер.УстановкаПараметровСеанса(ИменаПараметровСеанса);
	// Конец СтандартныеПодсистемы
	
	//ПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СпецификацияПользователей_ат.Контрагент,
		|	СпецификацияПользователей_ат.Основной
		|ИЗ
		|	РегистрСведений.СпецификацияПользователей_ат КАК СпецификацияПользователей_ат
		|ГДЕ
		|	СпецификацияПользователей_ат.Пользователь = &Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	СпецификацияПользователей_ат.Основной УБЫВ";
		
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	КонтрагентыПользователя = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Контрагент");
	
	ПараметрыСеанса.КонтрагентыПользователя_ат = Новый ФиксированныйМассив(КонтрагентыПользователя);
	
	//
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Метки_ат.Ссылка КАК ГруппаМеток
		|ИЗ
		|	Справочник.Метки_ат КАК Метки_ат
		|ГДЕ
		|	Метки_ат.ЭтоГруппа
		|	И (Метки_ат.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				ДоступностьМетокКлиентам_ат.ГруппаМеток
		|			ИЗ
		|				РегистрСведений.ДоступностьМетокКлиентам_ат КАК ДоступностьМетокКлиентам_ат
		|			ГДЕ
		|				(ДоступностьМетокКлиентам_ат.Клиент В (&Клиент)
		|					ИЛИ ДоступностьМетокКлиентам_ат.Пользователь = &Пользователь))
		|		ИЛИ Метки_ат.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				Метки_ат.Ссылка
		|			ИЗ
		|				Справочник.Метки_ат КАК Метки_ат
		|			ГДЕ
		|				Метки_ат.Общая))";
	
	Запрос.УстановитьПараметр("Клиент", КонтрагентыПользователя);
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	ГруппыМетокПользователя =  Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГруппаМеток");
	
	ПараметрыСеанса.ГруппыМетокПользователя_ат = Новый ФиксированныйМассив(ГруппыМетокПользователя);
	
	//
	
	ПараметрыСеанса.СотрудникОрганизации_ат = ВнутреннегоИспользования_ВызовСервера_ат.СотрудникОрганизации();
	
	//
	
	Если ПараметрыСеанса.СотрудникОрганизации_ат Тогда
		ВнутреннегоИспользования_ВызовСервера_ат.УстановитьЗначениеПоУмолчанию("КлиентДляОтбора", Неопределено);
	КонецЕсли;
	
	//
	
КонецПроцедуры

#КонецЕсли