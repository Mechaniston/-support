
&НаКлиенте
Процедура ЗаявкаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", Заявка);
	ПараметрыОткрытия.Вставить("ВводПрошлогоПериода", Истина);
	ОткрытьФорму("Документ.Заявка_ат.ФормаОбъекта", ПараметрыОткрытия , ЭтаФорма, , , , ,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкаСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ПослеВводаДаты", ЭтаФорма);
	ПоказатьВводДаты(Оповещение, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаДаты(ДатаЗаявки, ПередаваемыеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДатаЗаявки) Тогда
		Если ДатаЗаявки = НачалоДня(ДатаЗаявки) Тогда
			ДатаЗаявки = ДатаЗаявки + 1;
		КонецЕсли;
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ВводПрошлогоПериода", Истина);
		ПараметрыОткрытия.Вставить("ДатаЗаявки", ДатаЗаявки);
		ОткрытьФорму("Документ.Заявка_ат.ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма, , , , ,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если ТипЗнч(НовыйОбъект) = Тип("ДокументСсылка.Заявка_ат") Тогда
		Заявка = НовыйОбъект;
	ИначеЕсли ТипЗнч(НовыйОбъект) = Тип("ДокументСсылка.Задание_ат") Тогда
		НовоеЗадание = Задания.Добавить();
		НовоеЗадание.Задание = НовыйОбъект;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкаПриИзменении(Элемент)
	
	ЗаполнитьЗадания();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗадания()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СвязиОбъектов.Объект КАК Задание
		|ИЗ
		|	РегистрСведений.СвязиОбъектов_ат КАК СвязиОбъектов
		|ГДЕ
		|	СвязиОбъектов.Предок = &Заявка
		|	И СвязиОбъектов.Объект ССЫЛКА Документ.Задание_ат";
		
	Запрос.УстановитьПараметр("Заявка", Заявка);

	Задания.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаданияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", Элементы.Задания.ТекущиеДанные.Задание);
	ПараметрыОткрытия.Вставить("ВводПрошлогоПериода", Истина);
	ОткрытьФорму("Документ.Задание_ат.ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма, , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗадание(Команда)
	
	Если НЕ ЗначениеЗаполнено(Заявка) Тогда
		
		ПоказатьПредупреждение(, "Заявка не выбрана!", 5);
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Предок", Заявка);
	ПараметрыОткрытия.Вставить("ТипСвязи", ПредопределенноеЗначение("Справочник.СвязиОбъектов_ТипыСвязей_ат.ЗаданиеПодчиненоЗаявке"));
	ПараметрыОткрытия.Вставить("ВводПрошлогоПериода", Истина);
	
	ОткрытьФорму("Документ.Задание_ат.ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
