
// Проверяет наличие слов из переданного массива в регистре сведений слов-исключений.
//
// Параметры:
//	МассивСлов - Массив - Массив слов, которые необходимо проверить.
//
// Возвращаемое значение:
//  Массив - Массив слов не найденых в исключениях.
// 
Функция   ВернутьМассивСловНеСодержащихсяВПроизвольномСловаре(Знач МассивСлов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СловарьИсключенийДляПроверкиОрфографии_ат.Слово
		|ИЗ
		|	РегистрСведений.СловарьИсключенийДляПроверкиОрфографии_ат КАК СловарьИсключенийДляПроверкиОрфографии_ат
		|ГДЕ
		|	СловарьИсключенийДляПроверкиОрфографии_ат.Слово В(&МассивСлов)";
	
	Запрос.УстановитьПараметр("МассивСлов", МассивСлов);
	
	МассивИсключений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Слово");
	
	Для Каждого СловоИсключение Из МассивИсключений Цикл
		
		ИндексСлова = МассивСлов.Найти(СловоИсключение);
		
		Если ИндексСлова <> Неопределено Тогда
			
			МассивСлов.Удалить(ИндексСлова);	
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивСлов;
	
КонецФункции

// Добавляет слово в дополнительный словарь.
//
// Параметры:
//	Слово - Строка - Слово добавляемое в словарь.
// 
Процедура ДобавитьСловоВСловарь(Слово) Экспорт
	
	СловарьИсключений = РегистрыСведений.СловарьИсключенийДляПроверкиОрфографии_ат.СоздатьМенеджерЗаписи();
	СловарьИсключений.Слово = Слово;
	СловарьИсключений.Записать();
	
КонецПроцедуры
