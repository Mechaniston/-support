
Процедура ПередЗаписью(Отказ)
	
	Если ПустаяСтрока(НаименованиеВнутреннее) Тогда
		
		НаименованиеВнутреннее = Наименование;
		
	КонецЕсли;
	
	// Проверка на циклические ссылки
	Если НЕ ОтображениеКлиенту.Пустая() Тогда
		
		Если ОтображениеКлиенту = Ссылка Тогда
			
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Запрещено выбирать в <Отображение клиенту> сам элемент.";
			Сообщение.Сообщить();
			
		ИначеЕсли НЕ ОтображениеКлиенту.ОтображениеКлиенту.Пустая() Тогда
			
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Запрещено выбирать в <Отображение клиенту> элемент с заполненным отображением клиенту.";
			Сообщение.Сообщить();
			
		ИначеЕсли НЕ Ссылка.Пустая() Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ТипыЗаявок_ат.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.ТипыЗаявок_ат КАК ТипыЗаявок_ат
				|ГДЕ
				|	ТипыЗаявок_ат.ОтображениеКлиенту = &Ссылка";
			
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Если НЕ РезультатЗапроса.Пустой() Тогда
				
				Отказ = Истина;
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Данный Тип заявки выбран для отображения клиенту в другом Типе, "
									+ "не пустое значение в Отображение клиенту запрещено.";
				Сообщение.Сообщить();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
