
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДерево();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДерево(Клиенты = Неопределено)
	
	ДеревоЗначений = РеквизитФормыВЗначение("Дерево");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЛОЖЬ КАК Пометка,
		|	Проекты_ат.Ссылка,
		|	Проекты_ат.Наименование,
		|	ВЫРАЗИТЬ(ИсторияКомментариев_атСрезПоследних.Комментарий КАК СТРОКА(1000)) КАК Комментарий,
		|	Проекты_ат.Архивный,
		|	Проекты_ат.БудущихПериодов
		|ИЗ
		|	Справочник.Проекты_ат КАК Проекты_ат
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияКомментариев_ат.СрезПоследних КАК ИсторияКомментариев_атСрезПоследних
		|		ПО ИсторияКомментариев_атСрезПоследних.Ссылка = Проекты_ат.Ссылка
		|ГДЕ
		|	Проекты_ат.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				Проекты_ат.Ссылка
		|			ИЗ
		|				Справочник.Проекты_ат КАК Проекты_ат
		|			ГДЕ
		|				Проекты_ат.Родитель = ЗНАЧЕНИЕ(Справочник.Проекты_ат.ПустаяСсылка)
		|				И ВЫБОР
		|					КОГДА &ОтбиратьПоКлиентам
		|						ТОГДА Проекты_ат.Клиент В (&Клиенты)
		|								ИЛИ Проекты_ат.Клиент = ЗНАЧЕНИЕ(Справочник.Контрагенты_ат.ПустаяСсылка)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ)
		|"+ ?(Элементы.СкрытьАрхивные.Пометка, "И НЕ Проекты_ат.Архивный", "") +"
		|"+ ?(Элементы.СкрытьБудущие.Пометка, "И НЕ Проекты_ат.БудущихПериодов", "") +"
		|
		|УПОРЯДОЧИТЬ ПО
		|	Проекты_ат.Архивный ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("ОтбиратьПоКлиентам", Элементы.ОтборПоКлиентам.Пометка);
	Запрос.УстановитьПараметр("Клиенты", Клиенты);
	
	ДеревоЗначений = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Для каждого ВыбранныйПроект Из ВыбранныеПроекты Цикл
		
		СтрокаДерева = ДеревоЗначений.Строки.Найти(ВыбранныйПроект.Значение, , Истина);
		
		Если СтрокаДерева <> Неопределено Тогда
			
			СтрокаДерева.Пометка = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "Дерево");
	
КонецПроцедуры 

&НаКлиенте
Процедура СкрытьАрхивные(Команда)

	Элементы.СкрытьАрхивные.Пометка = НЕ Элементы.СкрытьАрхивные.Пометка;
	ЗаполнитьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьБудущие(Команда)
	
	Элементы.СкрытьБудущие.Пометка = НЕ Элементы.СкрытьБудущие.Пометка;
	ЗаполнитьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоКлиентам(Команда)
	
	Кнопка = Элементы.ОтборПоКлиентам;
	
	Если Кнопка.Пометка Тогда
		Кнопка.Пометка = Ложь;
		Кнопка.РасширеннаяПодсказка.Заголовок = "Установить отбор по клиентам";
		ЗаполнитьДерево();
	Иначе	
		СтруктураКлиентов = ОткрытьФормуМодально("Справочник.Контрагенты_ат.Форма.ФормаГрупповогоВыбора");
		Если СтруктураКлиентов <> Неопределено И СтруктураКлиентов.Клиенты.Количество() > 0 Тогда
			Кнопка.Пометка = Истина;
			Кнопка.РасширеннаяПодсказка.Заголовок = СтруктураКлиентов.Представление;
			ЗаполнитьДерево(СтруктураКлиентов.Клиенты);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПометкаПриИзменении(Элемент)
	
	Для Каждого ВыделеннаяСтрока Из ТекущийЭлемент.ВыделенныеСтроки Цикл
        Если ТипЗнч(ВыделеннаяСтрока) = Тип("Число") Тогда
            ДанныеСтроки = Элементы.Дерево.ДанныеСтроки(ВыделеннаяСтрока);
        Иначе
            ДанныеСтроки = ВыделеннаяСтрока;
		КонецЕсли;
		
		Если ДанныеСтроки <> ТекущийЭлемент.ТекущиеДанные Тогда // т.к. для текущий строки пометку меняет сама платформа
			ДанныеСтроки.Пометка = Не ДанныеСтроки.Пометка;
		КонецЕсли;
		
		ОбработатьИзменениеПометки(ДанныеСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеПометки(ЭлементДерева)
	
	ПроектВСписке = ВыбранныеПроекты.НайтиПоЗначению(ЭлементДерева.Ссылка);
	Пометка = ЭлементДерева.Пометка;
	Если Пометка И ПроектВСписке = Неопределено Тогда
		ВыбранныеПроекты.Добавить(ЭлементДерева.Ссылка, ЭлементДерева.Наименование);
	ИначеЕсли НЕ Пометка И ПроектВСписке <> Неопределено Тогда
		ВыбранныеПроекты.Удалить(ПроектВСписке);
	КонецЕсли;
	
	ДочерниеЭлементы = ЭлементДерева.ПолучитьЭлементы();
	
	Для Каждого Элемент Из ДочерниеЭлементы Цикл
		Элемент.Пометка = Пометка;
        ОбработатьИзменениеПометки(Элемент);
	КонецЦикла;
	
КонецПроцедуры 

&НаКлиенте
Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущийЭлемент.ТекущиеДанные.Пометка = НЕ ТекущийЭлемент.ТекущиеДанные.Пометка;
	ОбработатьИзменениеПометки(ТекущийЭлемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	//!!ТУДУ - нужно состояние флагов СкрытьАрхивные и СкрытьБудущихПериодов переносить как условие фильтра для списка
	// из основной формы, т.е. если в этой форме стоит галка "СА", ни один проект не выделен, и нажимаем Применить,
	// то в списке заданий/задач надо скрыть все с архивными основными проектами
	Если ВыбранныеПроекты.Количество() > 0 Тогда
		МассивВыбранных = Новый Массив;
		Представление = "Установлен отбор по " + ?(ВыбранныеПроекты.Количество() = 1, "проекту: ", "проектам: ");
		
		Для Каждого ВыбранныйПроект Из ВыбранныеПроекты Цикл
			МассивВыбранных.Добавить(ВыбранныйПроект.Значение);
			Представление = Представление + ВыбранныйПроект.Представление + "; ";
		КонецЦикла;
		Представление = Лев(Представление, СтрДлина(Представление) - 2) + "."; //!!ТУДУ: заменить на Строки_ат.ВернутьОбрезаннуюСКонца(Представление, 2)
		
		Закрыть(Новый Структура("Проекты, Представление", МассивВыбранных, Представление));
	Иначе	
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриАктивизацииСтроки(Элемент)
	
	Элементы.НадписьПроектКомментарий.Заголовок = Элемент.ТекущиеДанные.Комментарий;	
	
	//!ТУДУ -  Программно зафиксировать колонку почему-то не поучается :( А без этого не работают Enter и пробел с клавиатуры
	//			для изменения состояния пометки
	Элементы.Дерево.ТекущийЭлемент = Элементы.ДеревоПометка;
	
КонецПроцедуры
