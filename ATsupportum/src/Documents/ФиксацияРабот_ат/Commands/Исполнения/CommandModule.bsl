
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыФормы = Новый Структура;
	
	Если 	ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.Контрагенты_ат") Тогда
		
		ПараметрИмя = "Клиент";
		
	ИначеЕсли 	ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.Договоры_ат") Тогда
		
		ПараметрИмя = "Договор";
		
	ИначеЕсли 	ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.Задание_ат") Тогда
		
		ПараметрИмя = "Задания.Задание"; //Ссылка.Задания.Задание
		
	ИначеЕсли 	ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.Согласование_ат") Тогда
		
		ПараметрИмя = "Основания.Согласование"; //Ссылка.Основания.Согласование
		
	ИначеЕсли 	ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.Заявка_ат") Тогда
		
		ПараметрИмя = "Заявка"; //Ссылка.Заявка
		ПараметрыФормы.Вставить("Заявка", ПараметрКоманды);
		
	Иначе
		
		ПараметрИмя = "";
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПараметрИмя) Тогда
		
		фиксНастройки = Новый НастройкиКомпоновкиДанных;
		
		эОтбор = фиксНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		эОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПараметрИмя);
		эОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		эОтбор.ПравоеЗначение = ПараметрКоманды;
		эОтбор.Использование = Истина;
		//эОтбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
		ПараметрыФормы.Вставить("ФиксированныеНастройки", фиксНастройки);
		ПараметрыФормы.Вставить("НеСохранятьПользовательскиеНастройки", Истина);
		
		//ПараметрыФормы.Вставить("Отбор", Новый Структура(ПараметрИмя, ПараметрКоманды));
		
	Иначе
		
		ПараметрыФормы = Новый Структура();
		
	КонецЕсли;
	
	ОткрытьФорму("Документ.ФиксацияРабот_ат.ФормаСписка", ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно,
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
КонецПроцедуры
