
&НаКлиенте
Процедура ПерезаполнитьПриоритеты(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВопросаОПерезаполненииПриоритетов", ЭтотОбъект);
	
	ПоказатьВопрос(Оповещение, "Приоритеты всех Заявок будут пересчитаны. Продолжить?", РежимДиалогаВопрос.ДаНет,
		10, КодВозвратаДиалога.Нет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаОПерезаполненииПриоритетов(Результат, ПередаваемыеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПерезаполнитьПриоритеты_Сервер();
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПерезаполнитьПриоритеты_Сервер()
	
	НаборЗаписей = РегистрыСведений.ПриоритетЗаявок_ат.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсторияСтатусовЗаявок_атСрезПоследних.Ссылка КАК Ссылка,
		|	ИсторияСтатусовЗаявок_атСрезПоследних.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.ИсторияСтатусовЗаявок_ат.СрезПоследних(, НЕ Ссылка.ПометкаУдаления) КАК ИсторияСтатусовЗаявок_атСрезПоследних
		|ГДЕ
		|	(ИсторияСтатусовЗаявок_атСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявок_ат.Зарегистрирована)
		|			ИЛИ ИсторияСтатусовЗаявок_атСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявок_ат.НаСогласовании))
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИсторияСтатусовЗаявок_атСрезПоследних.Ссылка.Дата";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Планирование_Сервер_ат.УстановитьПриоритетЗаявки(Выборка.Ссылка, Выборка.Статус);
		
	КонецЦикла;
	
КонецПроцедуры
