
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолучитьДанныеВнешнейБД_Сервер();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеВнешнейБД_Сервер()
	
	ОбъектыВнешнейБД.Очистить();
	
	Подключение = ПодключениеКИБ_ат.УстановитьПодключение(Константы.БухгалтерияДляСинхронизации_ат.Получить());
	Если ТипЗнч(Подключение) = Тип("Строка") Тогда
		
		Сообщить(Подключение);
		Возврат;
		
	КонецЕсли;
	
	Запрос = Подключение.NewObject("Запрос");
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка,
		|	Контрагенты.Наименование,
		|	Контрагенты.НаименованиеПолное,
		|	Контрагенты.ИНН
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ Контрагенты.ПометкаУдаления
		|	И НЕ Контрагенты.ЭтоГруппа";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Строка = ОбъектыВнешнейБД.Добавить();
		
		Строка.Наименование 		= Выборка.Наименование;
		Строка.НаименованиеПолное 	= Выборка.НаименованиеПолное;
		Строка.ИНН 					= Выборка.ИНН;
		Строка.Идентификатор 		= ПодключениеКИБ_ат.ПолучитьGUIDВнешнейСсылки(Подключение, Выборка.Ссылка);
		Строка.НавигационнаяСсылка 	= Подключение.ПолучитьНавигационнуюСсылку(Выборка.Ссылка);
		
	КонецЦикла;
	
	ОбъектыВнешнейБД.Сортировать("Наименование");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыВнешнейБДВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВозвращаемаяСтруктура = Новый Структура;
	
	ВозвращаемаяСтруктура.Вставить("Идентификатор", 		Элемент.ТекущиеДанные.Идентификатор);
	ВозвращаемаяСтруктура.Вставить("НавигационнаяСсылка", 	Элемент.ТекущиеДанные.НавигационнаяСсылка);
	
	Закрыть(ВозвращаемаяСтруктура);
	
КонецПроцедуры
