
#Область ОбработчикиДействийПользователя

&НаКлиенте
Процедура НавигационнаяСсылкаПриИзменении(Элемент)
	
	Запись.Идентификатор = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
	
	Если НЕ ОбязательныеРеквизитыЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодбораОбъекта", ЭтаФорма);
	ОткрытьФорму("РегистрСведений.СинхронизацияКонтрагентов_ат.Форма.ФормаВыбораОбъектаВнешнейИБ",,
		ЭтаФорма,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Синхронизировать(Команда)
	
	Если НЕ ОбязательныеРеквизитыЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.НавигационнаяСсылка) Тогда
		
		РезультатСинхронизации = Синхронизировать_Сервер();
		
		Если ТипЗнч(РезультатСинхронизации) = Тип("Строка") Тогда
			ПоказатьПредупреждение(, РезультатСинхронизации, 5);
		КонецЕсли;
		
	Иначе
		
		ПоказатьПредупреждение(, "Навигационная ссылка не заполнена!", 5);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция   ОбязательныеРеквизитыЗаполнены()
	
	ТекстПредупреждения = "";
	
	Если Запись.Ссылка.Пустая() Тогда
		ТекстПредупреждения = "Контрагент должен быть выбран!";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, СокрЛ(ТекстПредупреждения), 5);
	КонецЕсли;
	
	Возврат ТекстПредупреждения = "";
	
КонецФункции 

&НаСервере
Функция   Синхронизировать_Сервер()
	
	ИБ = Константы.БухгалтерияДляСинхронизации_ат.Получить();
	
	Подключение = ПодключениеКИБ_ат.УстановитьПодключение(ИБ);
	Если ТипЗнч(Подключение) = Тип("Строка") Тогда
		Возврат Подключение;
	КонецЕсли;
	
	СинхронизированныйКонтрагент = ПодключениеКИБ_ат.ПолучитьВнешнююСсылкуПоНавигационнойСсылке(Подключение, Запись.НавигационнаяСсылка);
	Если СинхронизированныйКонтрагент = Неопределено Тогда
		Возврат "Введённая навигационная ссылка не корректна для выбранной ИБ Бухгалтерии Предприятия <" + ИБ + ">!";
	КонецЕсли;
	
	Запись.Идентификатор = Строки_КлиентСервер_ат.ПолучитьСтрУИдПоНавСсылке(Запись.НавигационнаяСсылка);
	
	Модифицированность = Истина;
	
	Возврат "Синхронизация прошла успешно";
	
КонецФункции

&НаКлиенте
Процедура ПослеПодбораОбъекта(Результат, ПередаваемыеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		Запись.Идентификатор = Результат.Идентификатор;
		Запись.НавигационнаяСсылка = Результат.НавигационнаяСсылка;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
