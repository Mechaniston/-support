////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗакрыватьПриЗакрытииВладельца = Истина;
	
	Заголовок = Параметры.Заголовок;
	
	Если Параметры.Свойство("АдресКомпоновщикаНастроек", АдресКомпоновщикаНастроек) Тогда
		// Хранилище приоритетнее
		КомпоновщикНастроек = ПолучитьИзВременногоХранилища(АдресКомпоновщикаНастроек);
	Иначе
		КомпоновщикНастроек = Параметры.КомпоновщикНастроек;
	КонецЕсли;
	
	Параметры.Свойство("ПериодДанных", ПериодДанных);
	
	Если Параметры.ВыборПериода Тогда
		ВыгружатьЗаПериод = ЗначениеЗаполнено(ПериодДанных);
	Иначе
		ВыгружатьЗаПериод = Ложь;
		// Отключаем выбор периода совсем
		Элементы.ВыгружатьЗаПериод.Видимость = Ложь;
		Элементы.ПериодДанных.Видимость      = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступностьПериодаОтбора();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ
//

&НаКлиенте
Процедура ПериодДанныхОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
//

&НаКлиенте
Процедура КомандаОК(Команда)
	ОповеститьОВыборе(РезультатВыбора());
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

&НаКлиенте
Процедура УстановитьДоступностьПериодаОтбора()
	Элементы.ПериодДанных.Доступность = ВыгружатьЗаПериод;
КонецПроцедуры

&НаСервере
Функция РезультатВыбора()
	Результат = Новый Структура;
	Результат.Вставить("ДействиеВыбора",      Параметры.ДействиеВыбора);
	Результат.Вставить("КомпоновщикНастроек", КомпоновщикНастроек);
	Результат.Вставить("ПериодДанных",        ?(ВыгружатьЗаПериод, ПериодДанных, Новый СтандартныйПериод));
	
	Результат.Вставить("АдресКомпоновщикаНастроек");
	Если Не ПустаяСтрока(АдресКомпоновщикаНастроек) Тогда
		Результат.АдресКомпоновщикаНастроек = ПоместитьВоВременноеХранилище(КомпоновщикНастроек, АдресКомпоновщикаНастроек);
	КонецЕсли;
		
	Возврат Результат;
КонецФункции
