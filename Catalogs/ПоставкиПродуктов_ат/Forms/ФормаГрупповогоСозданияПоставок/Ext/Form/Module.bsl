
//!!!!!HOTFIX

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Продукт) Тогда
		ОсновнойПродукт = Параметры.Продукт;
		ЗаполнитьТаблицуДочернихПродуктов();
	Иначе 
		Элементы.ДочерниеПродукты.Доступность = Ложь;
	КонецЕсли;
	
	СоздаватьЭкземпляры = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойПродуктПриИзменении(Элемент)

	Если ЗначениеЗаполнено(ОсновнойПродукт) Тогда
		ЗаполнитьТаблицуДочернихПродуктов();
	Иначе
		ДочерниеПродукты.Очистить();
		Элементы.ДочерниеПродукты.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("ИсключитьДочерниеПродуктыИКонфигурации", Истина);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораПродукта", ЭтаФорма);
	
	ОткрытьФорму("Справочник.Продукты_ат.ФормаВыбора", ПараметрыОткрытия,
		ЭтаФорма, УникальныйИдентификатор,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПродукта(Результат, ПередаваемыеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		НоваяСтрока = ДочерниеПродукты.Добавить();
		НоваяСтрока.Продукт = Результат;
		НоваяСтрока.Пометка = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДочернихПродуктов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА КАК Пометка,
	|	СвязиПродуктов_ат.ПодчиненныйПродукт
	|ИЗ
	|	РегистрСведений.СвязиПродуктов_ат КАК СвязиПродуктов_ат
	|ГДЕ
	|	СвязиПродуктов_ат.ОсновнойПродукт = &ОсновнойПродукт";
	
	Запрос.УстановитьПараметр("ОсновнойПродукт", ОсновнойПродукт);
	
	ДочерниеПродукты.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры 

&НаКлиенте
Процедура СоздатьПоставки(Команда)
	
	ТекстПредупреждения = "";
	
	Если НЕ ЗначениеЗаполнено(ОсновнойПродукт) Тогда
		
		ТекстПредупреждения = ТекстПредупреждения + ?(ЗначениеЗаполнено(ТекстПредупреждения), ", ", "") + """Основной продукт""";
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КонтрагентВладелец) Тогда
		
		ТекстПредупреждения = ТекстПредупреждения + ?(ЗначениеЗаполнено(ТекстПредупреждения), ", ", "") + """Контрагент владелец""";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		
		ТекстПредупреждения = "Необходимо заполнить: " + ТекстПредупреждения;
		ПоказатьПредупреждение(, ТекстПредупреждения, 10);
		
		Возврат;
		
	КонецЕсли;
	
	Если СоздатьПоставки_Сервер() Тогда
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция   СоздатьПоставки_Сервер()
	
	Попытка
		
		НачатьТранзакцию();
		
		СоздатьПоставку(ОсновнойПродукт, КонтрагентВладелец, СоздаватьЭкземпляры, Истина, ОписаниеПоставки);
		
		Для Каждого Строка Из ДочерниеПродукты Цикл
			
			Если НЕ Строка.Пометка Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			СоздатьПоставку(Строка.ПодчиненныйПродукт, КонтрагентВладелец, СоздаватьЭкземпляры,, Строка.Описание);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		Возврат Истина;
		
	Исключение
		
		Сообщить(ОписаниеОшибки());
		ОтменитьТранзакцию();
		
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

&НаСервере
Процедура СоздатьПоставку(Продукт, Владелец, СоздатьЭкземпляр, Основная = Ложь, Описание = "")
	
	НоваяПоставка = Справочники.ПоставкиПродуктов_ат.СоздатьЭлемент();
	НоваяПоставка.Продукт = Продукт;
	НоваяПоставка.Наименование = Строка(Продукт);
	НоваяПоставка.Описание = Описание;
	НоваяПоставка.Родитель = ОсновнаяПоставка;
	НоваяПоставка.КонтрагентВладелец = Владелец;
	НоваяПоставка.Записать();
	
	Если СоздатьЭкземпляр Тогда
		
		СоздатьЭкземплярыПродукта(НоваяПоставка.Ссылка, Продукт, Описание);
		
	КонецЕсли;
	
	Если Основная Тогда
		
		ОсновнаяПоставка = НоваяПоставка.Ссылка;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭкземплярыПродукта(Поставка, Продукт, Описание = "")
	
	КоличествоЭкземпляров = ?(Продукт.Количество < 2, 1, Продукт.Количество);
	
	Если Продукт.ТипПродукта = Перечисления.ТипыПродуктов_ат.Документация
		ИЛИ Продукт.ТипПродукта = Перечисления.ТипыПродуктов_ат.ДокументацияПП1С
		ИЛИ Продукт.ТипПродукта = Перечисления.ТипыПродуктов_ат.Книга
		Тогда
		
		Для Сч = 1 По КоличествоЭкземпляров Цикл
			
			НовыйЭкземпляр = СоздатьЭкземплярПродукта(Поставка, Продукт, 1,
				?(КоличествоЭкземпляров > 1, "часть " + Сч, ""));
			
		КонецЦикла;
		
	Иначе
		
		СоздатьЭкземплярПродукта(Поставка, Продукт, КоличествоЭкземпляров, Описание);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция   СоздатьЭкземплярПродукта(Поставка, Продукт, КоличествоЭкземпляров, Описание)
	
	НовыйЭкземпляр = Справочники.ЭкземплярыПродуктов_ат.СоздатьЭлемент();
	
	НовыйЭкземпляр.Наименование = Строка(Продукт);
	НовыйЭкземпляр.Описание = Описание;
	НовыйЭкземпляр.Владелец = Поставка;
	НовыйЭкземпляр.ТекущийПродукт = Продукт;
	НовыйЭкземпляр.Количество = КоличествоЭкземпляров;
	
	НовыйЭкземпляр.Записать();
	
	Возврат НовыйЭкземпляр.Ссылка;
	
КонецФункции
