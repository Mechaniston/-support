
Перем КонтрагентВладелецИзменен;

Процедура ПередЗаписью(Отказ)
	
	Если ЗначениеЗаполнено(Родитель) И КонтрагентВладелец <> Родитель.КонтрагентВладелец Тогда
		
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Контрагенты в родительской и дочерней поставке не могут отличаться!";
		Сообщение.Сообщить(); 
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Родитель) И Продукт.ТипПродукта = Перечисления.ТипыПродуктов_ат.ДокументацияПП1С Тогда	
		
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "У документации ПП 1С не может отсутствовать родительская поставка!";
		Сообщение.Сообщить(); 
		
	КонецЕсли;
	
	КонтрагентВладелецИзменен = (КонтрагентВладелец <> Ссылка.КонтрагентВладелец);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Родитель) И КонтрагентВладелецИзменен Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПоставкиПродуктов_ат.Ссылка
			|ИЗ
			|	Справочник.ПоставкиПродуктов_ат КАК ПоставкиПродуктов_ат
			|ГДЕ
			|	ПоставкиПродуктов_ат.Ссылка В ИЕРАРХИИ(&Ссылка)
			|	И НЕ ПоставкиПродуктов_ат.Ссылка = &Ссылка";
			
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
		
			ДочерняяПоставка = Выборка.Ссылка.ПолучитьОбъект();
			ДочерняяПоставка.КонтрагентВладелец = КонтрагентВладелец;
			ДочерняяПоставка.Записать();
		
		КонецЦикла;	 
		
	КонецЕсли;
	
КонецПроцедуры

КонтрагентВладелецИзменен = Ложь;
