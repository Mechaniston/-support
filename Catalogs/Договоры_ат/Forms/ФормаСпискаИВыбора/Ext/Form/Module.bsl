
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправляемыеФормы_Сервер_ат.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка, , , Элементы.ГруппаКоманднаяПанель);
	
	Если Параметры.Свойство("ТипДоговора") Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ТипДоговора", Параметры.ТипДоговора);
	КонецЕсли;
	
	ВнутреннегоИспользования_ВызовСервера_ат.УстановитьОтборПоКлиентуДляОтбора(ЭтаФорма,, "Владелец");
	
	УстановитьОтборПоАктуальным_Сервер(НЕ Параметры.Свойство("ПоказыватьВсе"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправляемыеФормы_Клиент_ат.ПриОткрытии(ЭтаФорма, Отказ);
	
	УстановитьНачалоДняВСписке();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	УправляемыеФормы_Клиент_ат.ПередЗакрытием(ЭтаФорма, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КлиентДляОтбораИзменен" И НЕ Элементы.Список.РежимВыбора Тогда
		
		ВнутреннегоИспользования_Клиент_ат.ИзменитьОтборПоКлиентуДляОтбора(Список, "Владелец", Параметр);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	УправляемыеФормы_КлиентСервер_ат.ВосстановитьОтборСписка(Список, Настройки, "Владелец");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьОтборПоАктуальным(Команда)
	
	УстановитьОтборПоАктуальным_Сервер(НЕ Элементы.ОтборПоАктуальным.Пометка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьНачалоДняВСписке()
	
	ТекущаяДата = ТекущаяДата();
	
	Список.Параметры.УстановитьЗначениеПараметра("НачалоЭтогоДня", НачалоДня(ТекущаяДата));
	
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбновлениеНачалаДня", КонецДня(ТекущаяДата + 1) - ТекущаяДата, Истина);
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработчикОжиданияОбновлениеНачалаДня()
	
	УстановитьНачалоДняВСписке();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоАктуальным_Сервер(ИспользоватьОтбор)
	
	Элементы.ОтборПоАктуальным.Пометка = ИспользоватьОтбор;
	
	УправляемыеФормы_КлиентСервер_ат.ИзменитьИспользованиеОтбораДинамическогоСписка(Список, "Только актуальные", ИспользоватьОтбор);
	
КонецПроцедуры

#КонецОбласти

#Область УниверсальныеОбработчикиДействий

&НаКлиенте
Процедура ОбработчикУниверсальныхДействий(Команда)
	
	УправляемыеФормы_Клиент_ат.ДополнительныеДействияФормы(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаСервере
Функция   ОбработчикУниверсальныхДействий_Сервер(Элемент) Экспорт
	
	Возврат УправляемыеФормы_Сервер_ат.ДополнительныеДействияФормы(ЭтаФорма, Команды[Элемент.Имя]);
	
КонецФункции

#КонецОбласти
