
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НавигационнаяСсылкаПриИзменении(Элемент)
	
	Запись.Идентификатор = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Синхронизировать(Команда)
	
	Если НЕ ОбязательныеРеквизитыЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.НавигационнаяСсылка) Тогда
		
		РезультатСинхронизации = Синхронизировать_Сервер();
		
		Если ТипЗнч(РезультатСинхронизации) = Тип("Строка") Тогда // ошибка
			ПоказатьПредупреждение(, РезультатСинхронизации, 5);
		КонецЕсли;
		
	Иначе
		
		ПоказатьПредупреждение(, "Навигационная ссылка не заполнена!", 5);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция   ОбязательныеРеквизитыЗаполнены()
	
	ТекстПредупреждения = "";
	
	Если Запись.Ссылка.Пустая() Тогда
		ТекстПредупреждения = "Поступление должно быть выбрано!";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, СокрЛ(ТекстПредупреждения), 5);
	КонецЕсли;
	
	Возврат ТекстПредупреждения = "";
	
КонецФункции

&НаСервере
Функция   Синхронизировать_Сервер()
	
	ИБ = Константы.БухгалтерияДляСинхронизации_ат.Получить();
	
	Подключение = ПодключениеКИБ_ат.УстановитьПодключение(ИБ);
	Если ТипЗнч(Подключение) = Тип("Строка") Тогда
		Возврат Подключение;
	КонецЕсли;
	
	СинхронизированноеПоступление = ПодключениеКИБ_ат.ПолучитьВнешнююСсылкуПоНавигационнойСсылке(Подключение, Запись.НавигационнаяСсылка);
	Если СинхронизированноеПоступление = Неопределено Тогда
		Возврат "Введённая навигационная ссылка не корректна для выбранной ИБ Бухгалтерии Предприятия <" + ИБ + ">!";
	КонецЕсли;
	
	Запись.Идентификатор = Строки_КлиентСервер_ат.ПолучитьСтрУИдПоНавСсылке(Запись.НавигационнаяСсылка);
	
	Модифицированность = Истина;
	
	Возврат "Синхронизация прошла успешно";
	
КонецФункции

#КонецОбласти
