
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Ссылка") Тогда
		
		Счет = Параметры.Отбор.Ссылка;
		Элементы.ГруппаОбновлениеДанных.Видимость = Истина;
		
		БазаСинхронизации = Константы.БухгалтерияДляСинхронизации_ат.Получить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	
	Если СчетОтменен(Счет) Тогда
		
		Оповещение = Новый ОписаниеОповещения("ОбновитьДанные_ПослеПредупреждения", ЭтаФорма);
		ПоказатьПредупреждение(Оповещение, "Счёт отменён, его статус не будет пересчитан автоматически!", 5);
		
	Иначе
		
		ОбновитьДанные_ПослеПредупреждения(Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные_ПослеПредупреждения(ПередаваемыеПараметры) Экспорт
	
	ОбновитьДанные_Сервер();
	Элементы.Список.Обновить();
	
	Оповестить("ОбновленыДанныеОплаты", Счет);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция   СчетОтменен(Счет)
	
	Возврат Финансы_ат.ПолучитьСтатусОплатыСчета(Счет) = Перечисления.СтатусыОплатыСчетов_ат.Отменен;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанные_Сервер()
	
	МассивОшибок = СинхронизацияСБП_ат.ОбновитьДанныеОплатыСчетовВ_БП(БазаСинхронизации, Счет);
	
	Для Каждого Ошибка Из МассивОшибок Цикл
		Сообщить(Ошибка);
	КонецЦикла;
	
КонецПроцедуры 
