
////////////////////////////////////////////////////////////////////////////////
// Работа с метаданными
// 
// Первый автор - Вячеслав 'Mechanist' А. Павлов (с) с 2008 г.
//
//////////////////////////////////////////////////////////////////////////////// 


// Возвращает строковое наименование "порядкового" реквизита путём поиска перебором:
// "Порядок", "Код", "Номер" или "Наименование". Если реквизит не найден, возвращается пустая строка.
// Предназаначена для организации сортировки в запросах.
Функция   ПолучитьРеквизитПорядкаДляОбъектаМетаданных(ОбъектМетаданных) Экспорт
	
	Если Метаданные.Перечисления.Содержит(ОбъектМетаданных) Тогда
		Возврат "Порядок";
	КонецЕсли;
	
	Если ОбъектМетаданных.Реквизиты.Найти("Порядок") <> Неопределено Тогда
		
		Возврат "Порядок";
		
	Иначе
		
		СтандартныеРеквизиты = ОбъектМетаданных.СтандартныеРеквизиты;
		Для Каждого Реквизит Из СтандартныеРеквизиты Цикл
			
			Если Реквизит.Имя = "Код" Тогда
				Возврат "Код";
			ИначеЕсли Реквизит.Имя = "Номер" Тогда
				Возврат "Номер";
			ИначеЕсли Реквизит.Имя = "Наименование" Тогда
				Возврат "Наименование";
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Неопределено;
		
	КонецЕсли;
		
КонецФункции

Функция   СуществуетРеквизит(СсылкаИлиОбъект, НаименованиеРеквизита) Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СсылкаИлиОбъект, НаименованиеРеквизита);
	
КонецФункции

Функция   ПолучитьСписокПодсистемПоСсылкеИлиМетаданным(СсылкаИлиМетаданные) Экспорт
	
	СписокПодсистем = Новый СписокЗначений;
	
	Попытка
		ОМ = СсылкаИлиМетаданные.Метаданные();
	Исключение
		ОМ = СсылкаИлиМетаданные;
	КонецПопытки;
	
	Для Каждого Подсистема Из Метаданные.Подсистемы Цикл
		ОбработкаДереваПодсистем(ОМ, СписокПодсистем, Подсистема);
	КонецЦикла;
	
	Возврат СписокПодсистем;
	
КонецФункции

// Возвращает соответствующее правилам имен уникальное имя ссылки ("." заменяются на "_", пробелы удаляются).
// Само имя состоит из полного имени метаданных + Ссылки приведенной к строке. Для перечислений так же выполняется
// поиск имени (а не синонима) при приведении ссылки к строке.
Функция   ИмяСсылки(Ссылка) Экспорт
	
	СсылкаМД = Ссылка.Метаданные();
	СсылкаСтр = Строка(Ссылка);
	
	Если Метаданные["Перечисления"].Содержит(СсылкаМД) Тогда
		Для Каждого ЭлементПеречисления Из Метаданные.Перечисления[СсылкаМД.Имя].ЗначенияПеречисления Цикл
			Если ЭлементПеречисления.Синоним = СсылкаСтр Тогда
				СсылкаСтр = ЭлементПеречисления.Имя;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат СтрЗаменить(СтрЗаменить(СсылкаМД.ПолноеИмя() + СсылкаСтр, ".", "_"), " ", "");
	
КонецФункции



////// Служебные

Процедура ОбработкаДереваПодсистем(ОбъектМетаданных, СписокПодсистем, ТекущаяПодсистема)
	
	Если ТекущаяПодсистема.Состав.Содержит(ОбъектМетаданных) Тогда
		СписокПодсистем.Добавить(ТекущаяПодсистема.Имя, ТекущаяПодсистема);
	КонецЕсли;
	
	Для Каждого Подсистема Из ТекущаяПодсистема.Подсистемы Цикл
		ОбработкаДереваПодсистем(ОбъектМетаданных, СписокПодсистем, Подсистема);
	КонецЦикла;
	
КонецПроцедуры
