
Процедура ЗагрузитьКомментарий(Форма, Ссылка, ИмяРеквизитаКомментария = "Комментарий",
	ИмяСтраницыКомментария = "Страница_Комментарий", ИмяЭлементаКомментария = "Комментарий") Экспорт
	
	ЗагрузитьКомментарийПоВиду(Форма, Ссылка, "Комментарий", ИмяРеквизитаКомментария,
		ИмяСтраницыКомментария, ИмяЭлементаКомментария);
	
КонецПроцедуры

Процедура СохранитьКомментарий(Форма, Ссылка, ИмяРеквизитаКомментария = "Комментарий",
	ИмяСтраницыРазмещенияКомментария = "Страница_Комментарий", ИмяЭлементаКомментария = "Комментарий") Экспорт
	
	СохранитьКомментарийПоВиду(Форма, Ссылка, "Комментарий", ИмяРеквизитаКомментария,
		ИмяСтраницыРазмещенияКомментария, ИмяЭлементаКомментария);
	
КонецПроцедуры

Процедура ЗагрузитьКомментарийВнутренний(Форма, Ссылка, ИмяРеквизитаКомментария = "КомментарийВнутренний",
	ИмяСтраницыКомментария = "Страница_КомментарийВнутренний", ИмяЭлементаКомментария = "КомментарийВнутренний") Экспорт
	
	ЗагрузитьКомментарийПоВиду(Форма, Ссылка, "КомментарийВнутренний", ИмяРеквизитаКомментария,
		ИмяСтраницыКомментария, ИмяЭлементаКомментария);
	
КонецПроцедуры

Процедура СохранитьКомментарийВнутренний(Форма, Ссылка, ИмяРеквизитаКомментария = "КомментарийВнутренний",
	ИмяСтраницыРазмещенияКомментария = "Страница_КомментарийВнутренний", ИмяЭлементаКомментария = "КомментарийВнутренний") Экспорт
	
	СохранитьКомментарийПоВиду(Форма, Ссылка, "КомментарийВнутренний", ИмяРеквизитаКомментария,
		ИмяСтраницыРазмещенияКомментария, ИмяЭлементаКомментария);
	
КонецПроцедуры

Процедура ЗагрузитьКомментарийКлиента(Форма, Ссылка, ИмяРеквизитаКомментария = "КомментарийКлиента",
	ИмяСтраницыКомментария = "Страница_КомментарийКлиента", ИмяЭлементаКомментария = "КомментарийКлиента") Экспорт
	
	ЗагрузитьКомментарийПоВиду(Форма, Ссылка, "КомментарийКлиента", ИмяРеквизитаКомментария,
		ИмяСтраницыКомментария, ИмяЭлементаКомментария);
	
КонецПроцедуры

Процедура СохранитьКомментарийКлиента(Форма, Ссылка, ИмяРеквизитаКомментария = "КомментарийКлиента",
	ИмяСтраницыРазмещенияКомментария = "Страница_КомментарийКлиента", ИмяЭлементаКомментария = "КомментарийКлиента") Экспорт
	
	СохранитьКомментарийПоВиду(Форма, Ссылка, "КомментарийКлиента", ИмяРеквизитаКомментария,
		ИмяСтраницыРазмещенияКомментария, ИмяЭлементаКомментария);
	
КонецПроцедуры

Процедура ЗагрузитьКомментарийСинхронизируемый(Форма, Ссылка, ИмяРеквизитаКомментария = "КомментарийСинхронизируемый",
	ИмяСтраницыКомментария = "Страница_КомментарийСинхронизируемый", ИмяЭлементаКомментария = "КомментарийСинхронизируемый") Экспорт
	
	ЗагрузитьКомментарийПоВиду(Форма, Ссылка, "КомментарийСинхронизируемый", ИмяРеквизитаКомментария,
		ИмяСтраницыКомментария, ИмяЭлементаКомментария);
	
КонецПроцедуры

Процедура СохранитьКомментарийСинхронизируемый(Форма, Ссылка, ИмяРеквизитаКомментария = "КомментарийСинхронизируемый",
	ИмяСтраницыРазмещенияКомментария = "Страница_КомментарийСинхронизируемый", ИмяЭлементаКомментария = "КомментарийСинхронизируемый") Экспорт
	
	СохранитьКомментарийПоВиду(Форма, Ссылка, "КомментарийСинхронизируемый", ИмяРеквизитаКомментария,
		ИмяСтраницыРазмещенияКомментария, ИмяЭлементаКомментария);
	
КонецПроцедуры


/////////

Процедура ЗагрузитьКомментарийПоВиду(Форма, Ссылка, ВидКомментария, ИмяРеквизитаКомментария, ИмяСтраницыКомментария, ИмяЭлементаКомментария)
	
	ИмяСобытия = "Комментарии_ат.Загрузка комментария";
	
	ИменаКорректны =
		ИменаКорректны(Форма, Ссылка, ИмяРеквизитаКомментария, ИмяСтраницыКомментария, ИмяЭлементаКомментария, ИмяСобытия);
	
	Если НЕ ИменаКорректны Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРегистра = ИмяРегистраПоВидуКомментария(Ссылка, ВидКомментария, ИмяСобытия, , Истина);
	
	Если ИмяРегистра = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Просмотр", Метаданные.РегистрыСведений[ИмяРегистра]) Тогда
		
		Форма.Элементы[ИмяЭлементаКомментария].Видимость = Ложь;
		
		Возврат;
		
	ИначеЕсли НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений[ИмяРегистра]) Тогда
		
		Форма.Элементы[ИмяЭлементаКомментария].ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Форма[ИмяРеквизитаКомментария] = ПолучитьКомментарийПоВиду(Ссылка, ВидКомментария, ИмяСобытия);
	
	Если ИмяСтраницыКомментария <> Неопределено Тогда
		
		Форма.Элементы[ИмяСтраницыКомментария].Картинка =
			КартинкаКомментария(Форма[ИмяРеквизитаКомментария], ВРег(ВидКомментария) = ВРег("КомментарийВнутренний"));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьКомментарийПоВиду(Форма, Ссылка, ВидКомментария, ИмяРеквизитаКомментария, ИмяСтраницыКомментария, ИмяЭлементаКомментария)
	
	ИмяСобытия = "Комментарии_ат.Сохранение комментария";
	
	ИменаКорректны =
		ИменаКорректны(Форма, Ссылка, ИмяРеквизитаКомментария, ИмяСтраницыКомментария, ИмяЭлементаКомментария, ИмяСобытия);
	
	Если НЕ ИменаКорректны Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРегистра = ИмяРегистраПоВидуКомментария(Ссылка, ВидКомментария, ИмяСобытия, , Истина);
	
	Если ИмяРегистра = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Просмотр", Метаданные.РегистрыСведений[ИмяРегистра])
	 ИЛИ НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений[ИмяРегистра]) Тогда
		Возврат;
	КонецЕсли;
	
	СтарыйКомментарий = ПолучитьКомментарийПоВиду(Ссылка, ВидКомментария, ИмяСобытия);
	
	Если СтарыйКомментарий <> Форма[ИмяРеквизитаКомментария] Тогда
		ЗаписатьКомментарийПоВиду(Ссылка, ВидКомментария, Форма[ИмяРеквизитаКомментария], ИмяСобытия);
	КонецЕсли;
	
	Если ИмяСтраницыКомментария <> Неопределено Тогда
		
		Форма.Элементы[ИмяСтраницыКомментария].Картинка =
			КартинкаКомментария(Форма[ИмяРеквизитаКомментария], ВРег(ВидКомментария) = ВРег("КомментарийВнутренний"));
		
	КонецЕсли;
	
КонецПроцедуры

Функция   ПолучитьКомментарийПоВиду(Ссылка, ВидКомментария,
	ИмяСобытия = "Комментарии_ат.Загрузка комментария", ТекстОшибки = "", ВыводитьСообщениеПользователю = Истина) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ВРег(ВидКомментария) = ВРег("Комментарий") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ИсторияКомментариев_атСрезПоследних.Комментарий
			|ИЗ
			|	РегистрСведений.ИсторияКомментариев_ат.СрезПоследних(, Ссылка = &Ссылка) КАК ИсторияКомментариев_атСрезПоследних";
		
	ИначеЕсли ВРег(ВидКомментария) = ВРег("КомментарийВнутренний") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЕСТЬNULL(ИсторияКомментариевВнутренних_атСрезПоследних.КомментарийВHTML, ""<HTML><HEAD></HEAD><BODY><P></P></BODY></HTML>"") КАК Комментарий
			|ИЗ
			|	(ВЫБРАТЬ
			|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПОДСТРОКА(ИсторияКомментариевВнутренних_атСрезПоследних.КомментарийВHTML, 1, 1)) КАК Комментарий
			|	ИЗ
			|		РегистрСведений.ИсторияКомментариевВнутренних_ат.СрезПоследних(, Ссылка = &Ссылка) КАК ИсторияКомментариевВнутренних_атСрезПоследних) КАК ВложенныйЗапрос
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияКомментариевВнутренних_ат.СрезПоследних(, Ссылка = &Ссылка) КАК ИсторияКомментариевВнутренних_атСрезПоследних
			|			ПО (ИСТИНА)";
		
	ИначеЕсли ВРег(ВидКомментария) = ВРег("КомментарийКлиента") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ИсторияКомментариевКлиентов_атСрезПоследних.Комментарий
			|ИЗ
			|	РегистрСведений.ИсторияКомментариевКлиентов_ат.СрезПоследних(, Ссылка = &Ссылка) КАК ИсторияКомментариевКлиентов_атСрезПоследних";
		
	ИначеЕсли ВРег(ВидКомментария) = ВРег("КомментарийСинхронизируемый") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ИсторияКомментариевСинхронизируемых_атСрезПоследних.Комментарий
			|ИЗ
			|	РегистрСведений.ИсторияКомментариевСинхронизируемых_ат.СрезПоследних(, Ссылка = &Ссылка) КАК ИсторияКомментариевСинхронизируемых_атСрезПоследних";
		
	Иначе
		
		ТекстОшибки = "Ошибка архитектуры кода! Некорректный вид комментария: """ + ВидКомментария + """";
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, Ссылка.Метаданные(), Ссылка, ТекстОшибки);
		
		Если ВыводитьСообщениеПользователю Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстОшибки;
			Сообщение.Сообщить();
			
		КонецЕсли;
		
		Возврат "";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Комментарий;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Процедура ЗаписатьКомментарийПоВиду(Ссылка, ВидКомментария, Комментарий,
	ИмяСобытия = "Комментарии_ат.Сохранение комментария", ТекстОшибки = "", ВыводитьСообщениеПользователю = Истина) Экспорт
	
	ИмяРегистра = ИмяРегистраПоВидуКомментария(Ссылка, ВидКомментария, ИмяСобытия, ТекстОшибки, ВыводитьСообщениеПользователю);
	
	Если ИмяРегистра = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Период = ТекущаяДатаСеанса();
	
	МенеджерЗаписи = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Ссылка = Ссылка;
	МенеджерЗаписи.Период = Период;
	МенеджерЗаписи.Прочитать(); //!!! ересь-то какая. какой ещё одинаковый период равный ТДС() у двух записей?! переделать на получение среза последних
	
	Если МенеджерЗаписи.Выбран() Тогда // сливаем комментарии с одинаковым периодом
		
		Если ВРег(ВидКомментария) = ВРег("КомментарийВнутренний") Тогда
			
			Если МенеджерЗаписи.КомментарийВHTML = Комментарий Тогда
				Возврат;
			КонецЕсли;
			
			МенеджерЗаписи.КомментарийВHTML = РаботаСHTML_КлиентСервер_ат.ОбъединитьТекстыHTML(МенеджерЗаписи.КомментарийВHTML, Комментарий);
			
		Иначе
			
			Если МенеджерЗаписи.Комментарий = Комментарий Тогда
				Возврат;
			КонецЕсли;
			
			МенеджерЗаписи.Комментарий = МенеджерЗаписи.Комментарий + Символы.ПС + Символы.ПС + Комментарий;
			
		КонецЕсли;
		
	Иначе
		
		Если ВРег(ВидКомментария) = ВРег("КомментарийВнутренний") Тогда
			МенеджерЗаписи.КомментарийВHTML = Комментарий;
		Иначе
			МенеджерЗаписи.Комментарий = Комментарий;
		КонецЕсли;
		
	КонецЕсли;
	
	МенеджерЗаписи.Ссылка = Ссылка;
	МенеджерЗаписи.Период = Период;
	МенеджерЗаписи.Автор  = Пользователи.ТекущийПользователь();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура ДобавитьАвтокомментарийПоВиду(Ссылка, Комментарий) Экспорт
	
	Если ПустаяСтрока(Комментарий) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСобытия = "Комментарии_ат.Сохранение автокомментария";
	ВидКомментария = "Комментарий";
	ИмяРегистра = ИмяРегистраПоВидуКомментария(Ссылка, ВидКомментария, ИмяСобытия,, Истина);
	
	СтарыйКомментарий = ПолучитьКомментарийПоВиду(Ссылка, ВидКомментария, ИмяСобытия);
	
	Если Лев(СтарыйКомментарий, СтрДлина(Комментарий)) <> Комментарий Тогда
		ЗаписатьКомментарийПоВиду(Ссылка, ВидКомментария,
			Комментарий + " [" + Формат(ТекущаяДатаСеанса(), "ДФ='dd.MM.yyyy HH:mm'") + "]"
				+ ?(НЕ ПустаяСтрока(СтарыйКомментарий), Символы.ПС + Символы.ПС + СтарыйКомментарий, ""));
	КонецЕсли;
	
КонецПроцедуры

Функция   КартинкаКомментария(Комментарий, ПереданТекстHTML) Экспорт
	
	Если ПереданТекстHTML Тогда
		
		КомментарийПустой = РаботаСHTML_КлиентСервер_ат.ПустоеСодержаниеHTML(Комментарий);
		
	Иначе
		
		КомментарийПустой = ПустаяСтрока(Комментарий);
		
	КонецЕсли;
	
	Если КомментарийПустой Тогда
		
		Картинка = Новый Картинка;
		
	Иначе
		
		Картинка = БиблиотекаКартинок.Комментарий;
		
	КонецЕсли;
	
	Возврат Картинка;
	
КонецФункции

Функция   ИменаКорректны(Форма, Ссылка, ИмяРеквизитаКомментария, ИмяСтраницыКомментария, ИмяЭлементаКомментария, ИмяСобытия)
	
	ИмяРеквизитаКомментарияКорректно = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяРеквизитаКомментария);
	
	Если НЕ ИмяРеквизитаКомментарияКорректно Тогда
		
		ТекстОшибки = "Ошибка архитектуры кода! Некорректное имя реквизита комментария: """ + ИмяРеквизитаКомментария + """";
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, Ссылка.Метаданные(), Ссылка, ТекстОшибки);
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстОшибки;
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	Если ИмяСтраницыКомментария = Неопределено Тогда
		
		ИмяСтраницыКорректно = Истина;
		
	Иначе
		
		ИмяСтраницыКорректно = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, ИмяСтраницыКомментария);
		
		Если НЕ ИмяСтраницыКорректно Тогда
			
			ТекстОшибки = "Ошибка архитектуры кода! Некорректное имя страницы комментария: """ + ИмяСтраницыКомментария + """";
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, Ссылка.Метаданные(), Ссылка, ТекстОшибки);
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстОшибки;
			Сообщение.Сообщить();
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИмяЭлементаКомментарияКорректно = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяЭлементаКомментария);
	
	Если НЕ ИмяЭлементаКомментарияКорректно Тогда
		
		ТекстОшибки = "Ошибка архитектуры кода! Некорректное имя элемента формы комментария: """ + ИмяЭлементаКомментария + """";
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, Ссылка.Метаданные(), Ссылка, ТекстОшибки);
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстОшибки;
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	Возврат ИмяРеквизитаКомментарияКорректно И ИмяСтраницыКорректно И ИмяЭлементаКомментарияКорректно;
	
КонецФункции

Функция   ИмяРегистраПоВидуКомментария(Ссылка, ВидКомментария, ИмяСобытия, ТекстОшибки = "", ВыводитьСообщениеПользователю = Истина)
	
	Если ВРег(ВидКомментария) = ВРег("Комментарий") Тогда
		
		ИмяРегистра = "ИсторияКомментариев_ат";
		
	ИначеЕсли ВРег(ВидКомментария) = ВРег("КомментарийВнутренний") Тогда
		
		ИмяРегистра = "ИсторияКомментариевВнутренних_ат";
		
	ИначеЕсли ВРег(ВидКомментария) = ВРег("КомментарийКлиента") Тогда
		
		ИмяРегистра = "ИсторияКомментариевКлиентов_ат";
		
	ИначеЕсли ВРег(ВидКомментария) = ВРег("КомментарийСинхронизируемый") Тогда
		
		ИмяРегистра = "ИсторияКомментариевСинхронизируемых_ат"
		
	Иначе
		
		ТекстОшибки = "Ошибка архитектуры кода! Некорректный вид комментария: """ + ВидКомментария + """";
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, Ссылка.Метаданные(), Ссылка, ТекстОшибки);
		
		Если ВыводитьСообщениеПользователю Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстОшибки;
			Сообщение.Сообщить();
			
		КонецЕсли;
		
		ИмяРегистра = Неопределено;
		
	КонецЕсли;
	
	Возврат ИмяРегистра;
	
КонецФункции
