
// Открывает форму для выбора месяца и устанавливает выбранное значение в указанные реквизиты.
//
// Параметры:
//	Форма - УправляемаяФорма, Форма - Форма в которой из которой вызвана процедура для установки значений реквизитов.
//	Период - Дата - Дата для начального определения периода.
//	ПутьРеквизита - Строка - Путь к реквизиту, в который будет установлен выбранный период, например: "Объект.Дата".
//	ПутьРеквизитаПредставления - Строка - Путь к реквизиту, в который будет установленно представление выбранного периода, например: "Объект.ПредставлениеДаты".
//	ОповещениеЗавершения - ОписаниеОповещения -  Не обязательный параметр. Описание процедуры, которая будет вызвана после выбора периода.
//	ИзменитьМодифицированность - Булево - Определяет необходимост изменения модифицированности переданной формы.
//
Процедура ВыбратьМесяц(Форма, Период, ПутьРеквизита, ПутьРеквизитаПредставления,
	ОповещениеЗавершения = Неопределено, ИзменитьМодифицированность = Истина) Экспорт
	
	ВыбратьПериод(Форма, Период, ПутьРеквизита, ПутьРеквизитаПредставления, "Месяц",
		ОповещениеЗавершения, ИзменитьМодифицированность);	
	
КонецПроцедуры

// Открывает форму для выбора квартала и устанавливает выбранное значение в указанные реквизиты.
//
// Параметры:
//	Форма - УправляемаяФорма, Форма - Форма в которой из которой вызвана процедура для установки значений реквизитов.
//	Период - Дата - Дата для начального определения периода.
//	ПутьРеквизита - Строка - Путь к реквизиту, в который будет установлен выбранный период, например: "Объект.Дата".
//	ПутьРеквизитаПредставления - Строка - Путь к реквизиту, в который будет установленно представление выбранного периода, например: "Объект.ПредставлениеДаты".
//	ОповещениеЗавершения - ОписаниеОповещения -  Не обязательный параметр. Описание процедуры, которая будет вызвана после выбора периода.
//	ИзменитьМодифицированность - Булево - Определяет необходимост изменения модифицированности переданной формы.
//
Процедура ВыбратьКвартал(Форма, Период, ПутьРеквизита, ПутьРеквизитаПредставления,
	ОповещениеЗавершения = Неопределено, ИзменитьМодифицированность = Истина) Экспорт
	
	ВыбратьПериод(Форма, Период, ПутьРеквизита, ПутьРеквизитаПредставления, "Квартал",
		ОповещениеЗавершения, ИзменитьМодифицированность);	
	
КонецПроцедуры

// Открывает форму для выбора полугодия и устанавливает выбранное значение в указанные реквизиты.
//
// Параметры:
//	Форма - УправляемаяФорма, Форма - Форма в которой из которой вызвана процедура для установки значений реквизитов.
//	Период - Дата - Дата для начального определения периода.
//	ПутьРеквизита - Строка - Путь к реквизиту, в который будет установлен выбранный период, например: "Объект.Дата".
//	ПутьРеквизитаПредставления - Строка - Путь к реквизиту, в который будет установленно представление выбранного периода, например: "Объект.ПредставлениеДаты".
//	ОповещениеЗавершения - ОписаниеОповещения -  Не обязательный параметр. Описание процедуры, которая будет вызвана после выбора периода.
//	ИзменитьМодифицированность - Булево - Определяет необходимост изменения модифицированности переданной формы.
//
Процедура ВыбратьПолугодие(Форма, Период, ПутьРеквизита, ПутьРеквизитаПредставления,
	ОповещениеЗавершения = Неопределено, ИзменитьМодифицированность = Истина) Экспорт
	
	ВыбратьПериод(Форма, Период, ПутьРеквизита, ПутьРеквизитаПредставления, "Полугодие",
		ОповещениеЗавершения, ИзменитьМодифицированность);	
	
КонецПроцедуры

Процедура ВыбратьПериод(Форма, Период, ПутьРеквизита, ПутьРеквизитаПредставления, РежимВыбораПериода,
	ОповещениеЗавершения, ИзменитьМодифицированность)
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("Форма", Форма);
	ПередаваемыеПараметры.Вставить("ПутьРеквизита", ПутьРеквизита);
	ПередаваемыеПараметры.Вставить("ПутьРеквизитаПредставления", ПутьРеквизитаПредставления);
	ПередаваемыеПараметры.Вставить("ИзменитьМодифицированность", ИзменитьМодифицированность);
	ПередаваемыеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораПериода", ЭтотОбъект, ПередаваемыеПараметры);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Период", Период);
	ПараметрыОткрытия.Вставить("РежимВыбораПериода", РежимВыбораПериода);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораПериода_ат", ПараметрыОткрытия, Форма, , , ,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

Процедура ПослеВыбораПериода(Результат, ПередаваемыеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ОбщегоНазначения_КлиентСервер_ат.УстановитьЗначениеРеквизитаФормыПоПути(ПередаваемыеПараметры.Форма,
			ПередаваемыеПараметры.ПутьРеквизита, Результат.Период);
		ОбщегоНазначения_КлиентСервер_ат.УстановитьЗначениеРеквизитаФормыПоПути(ПередаваемыеПараметры.Форма,
			ПередаваемыеПараметры.ПутьРеквизитаПредставления, Результат.ПредставлениеПериода);
		
		Если ПередаваемыеПараметры.ИзменитьМодифицированность Тогда 
			
			ПередаваемыеПараметры.Форма.Модифицированность = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПередаваемыеПараметры.ОповещениеЗавершения = Неопределено Тогда
		
		ПередаваемыеПараметры.Форма.ОбновитьОтображениеДанных();
		
	Иначе	
		
		ВыполнитьОбработкуОповещения(ПередаваемыеПараметры.ОповещениеЗавершения, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры
