
Процедура ПересчитатьСумму(Строка, СуммаВключаетНДС = Ложь, ЗначениеПустогоКоличества = 0) Экспорт
	
	Строка.Сумма = Строка.Цена * ?(Строка.Количество = 0, ЗначениеПустогоКоличества, Строка.Количество);
	Если Строка.Свойство("СтавкаНДС") И Строка.Свойство("Всего") Тогда
		ПересчитатьСуммуНДС(Строка, СуммаВключаетНДС);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересчитатьСуммуНДС(Строка, СуммаВключаетНДС) Экспорт
	
	Строка.СуммаНДС = РассчитатьСуммуНДС(Строка.Сумма, СуммаВключаетНДС, ПолучитьСтавкуНДС(Строка.СтавкаНДС));
	Строка.Всего 	= Строка.Сумма + ?(СуммаВключаетНДС, 0, Строка.СуммаНДС);
	
КонецПроцедуры

// Рассчитывает сумму НДС исходя из суммы и флагов налогообложения
//
// Параметры: 
//  Сумма            - число, сумма от которой надо рассчитывать налоги, 
//  СуммаВключаетНДС - булево, признак включения НДС в сумму ("внутри" или "сверху"),
//  СтавкаНДС        - число , процентная ставка НДС,
//
// Возвращаемое значение:
//  Число, полученная сумма НДС
//
Функция   РассчитатьСуммуНДС(Сумма, СуммаВключаетНДС, СтавкаНДС) Экспорт
	
	Если СуммаВключаетНДС Тогда
		СуммаБезНДС = 100 * Сумма / (100 + СтавкаНДС);
		СуммаНДС = Сумма - СуммаБезНДС;
	Иначе
		СуммаБезНДС = Сумма;
	КонецЕсли;

	Если НЕ СуммаВключаетНДС Тогда
		СуммаНДС = СуммаБезНДС * СтавкаНДС / 100;
	КонецЕсли;
	
	Возврат СуммаНДС;
	
КонецФункции

Функция   ПолучитьСтавкуНДС(СтавкаНДС) Экспорт
	
	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС_ат.НДС20")
		ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС_ат.НДС20_120")
		Тогда
		
		Возврат 20;
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС_ат.НДС10")
		ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС_ат.НДС10_110")
		Тогда
		
		Возврат 10;
		
	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС_ат.НДС18")
		ИЛИ СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС_ат.НДС18_118")
		Тогда
		
		Возврат 18;
		
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции
